
faust stereo_echo
{
    import("stdfaust.lib");

    echo(del, feedback, gain) = + ~ de.delay(50000,d) * feedback * gain
    with {
        d = del*ma.SR;
    };

    process = echo(1.6, 0.6, 0.7),echo(0.5, 0.9, 0.5);
}

processor ClassicRingtone
{
    output stream float out;

    void run()
    {
        int[] pitches    = (76, 74, 66, 68, 73, 71, 62, 64, 71, 69, 61, 64, 69);
        int[] durations  = ( 1,  1,  2,  2,  1,  1,  2,  2,  1,  1,  2,  2,  4);

        let samplesPerQuarterNote = int (processor.frequency / 7);
        float sinewavePhase;

        loop {
            for (int noteIndex = 0; noteIndex < pitches.size; ++noteIndex)
            {
                let noteFrequency  = soul::noteNumberToFrequency (pitches.at (noteIndex));
                let noteLength     = samplesPerQuarterNote * durations.at (noteIndex);
                let phaseIncrement = float (noteFrequency * twoPi * processor.period);

                loop (noteLength)  // play our next note...
                {
                    out << 0.9f * sin (sinewavePhase);
                    sinewavePhase = addModulo2Pi (sinewavePhase, phaseIncrement);
                    advance();
                }
            }
        }
    }
}

faust osc
{
    import("stdfaust.lib");

    vol = hslider("volume [unit:dB]", -20, -96, 0, 0.1) : ba.db2linear : si.smoo;
    freq = hslider("freq [unit:Hz]", 1000, 20, 5000, 1);

    process = vgroup("Oscillator", os.osci(freq) * vol);
}

graph sequence [[main]]
{
    input event float32 eventfHslider1 [[ name: "freq", path: "/Oscillator/freq", min: 20.0f, max: 5000.0f, init: 1000.0f, step: 0.01f, unit: "Hz" ]];
    input event float32 eventfHslider0 [[ name: "volume", path: "/Oscillator/volume", min: -96.0f, max: 0.0f, init: -20.0f, step: 0.100000001f, unit: "dB" ]];

    output stream float audioOut0;
    output stream float audioOut1;

    connection 
    {
        eventfHslider1 -> osc.eventfHslider1;
        eventfHslider0 -> osc.eventfHslider0;

        osc.output0 -> stereo_echo.input0;
        ClassicRingtone.out -> stereo_echo.input1;

        stereo_echo.output0 -> audioOut0;
        stereo_echo.output1 -> audioOut1;
    }
}
